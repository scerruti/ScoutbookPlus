import org.ajoberstar.grgit.*

plugins {
    id 'org.ajoberstar.grgit' version '1.4.2'
}

apply plugin: 'com.android.application'

if(project.hasProperty("ScoutbookSecure.signing")
        && new File(project.property("ScoutbookSecure.signing") + ".gradle").exists()) {
    apply from: project.property("ScoutbookSecure.signing") + ".gradle";
}

repositories {
    jcenter()
}

ext {
    git = Grgit.open()
    gitVersionCode = git.tag.list().size() + 2
    gitVersionName = "${git.describe()}"
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "com.otabi.scoutbooksecure"
        minSdkVersion 19
        targetSdkVersion 23
        versionCode gitVersionCode
        versionName gitVersionName
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.robertoestivill.intentbuilder:intentbuilder:0.1.1'
}